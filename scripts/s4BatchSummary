#!/usr/bin/awk -f 
# usage:
#     s4BatchSummary s4_res.1.out [s4_res.2.out ....]
#
# This script will go though all the given s4 output
# data report a summary word error rate for all files.
#
# This is useful when tests have been divided into separate
# batches and summary info is desired.


$1 == "Words:" && $3 == "Matches:" {
   words[FILENAME] = $2;
}

$1== "Accuracy:" && $3 == "Errors:" {
   errors[FILENAME] = $4;
}



END {
    printf("%6.6s %6.6s %7.7s %s\n", "Words", "Errors", "WER%", "File");
    printf("==================================\n");
    for (i in words) {
        sumWords += words[i];
        sumErrors += errors[i];
        if (words[i] > 0) {
            wer = (errors[i]/words[i]) * 100.0;
            printf("%6d %6d %6.2f%% %s\n",  words[i], errors[i], wer, i);
        }
    }
    printf("==================================\n");
    wer = (sumErrors/sumWords) * 100.0;
    printf("%6d %6d %6.2f%% %s\n",  sumWords, sumErrors, wer, "total");
}

