#!/usr/bin/awk -f 
# usage:
#     s4BatchSummary s4_res.1.out [s4_res.2.out ....]
#
# This script will go though all the given s4 output
# data report a summary word error rate for all files.
#
# This is useful when tests have been divided into separate
# batches and summary info is desired.


$1 == "Words:" && $3 == "Matches:" {
   words[FILENAME] = $2;
   lines[FILENAME]++;
}

$1== "Accuracy:" && $3 == "Errors:" {
   errors[FILENAME] = $4;
}



END {
    printf("%6.6s %6.6s %6.6s %7.7s %s\n", \
        "Lines", "Words", "Errors", "WER%", "File");
    printf("==================================\n");
    for (i in words) {
        sumWords += words[i];
        sumErrors += errors[i];
        sumLines += lines[i];
        if (words[i] > 0) {
            wer = (errors[i]/words[i]) * 100.0;
            printf("%6d %6d %6d %6.2f%% %s\n",  \
                lines[i], words[i], errors[i], wer, i);
        }
    }
    printf("==================================\n");
    wer = (sumErrors/sumWords) * 100.0;
    printf("%6d %6d %6d %6.2f%% %s\n",  \
        sumLines, sumWords, sumErrors, wer, "total");
}

