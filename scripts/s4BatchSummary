#!/usr/bin/awk -f 
# usage:
#     s4BatchSummary s4_res.1.out [s4_res.2.out ....]
#
# This script will go though all the given s4 output
# data report a summary word error rate for all files.
#
# This is useful when tests have been divided into separate
# batches and summary info is desired.

BEGIN {
    printf("%6.6s %6.6s %s\n", "Words", "Errors", "File");
    printf("==================================\n");
}

$3 == "Summary" && $4 == "statistics" {
    inSummary = 1;
}


inSummary == 1 && $1 == "Words:" {
   totalWords = $2;
}

inSummary == 1 && $3 == "Errors:" {
   totalErrors = $4;
}


inSummary == 1 && $2 == "statistics" && $3 == "for" {
    inSummary = 0;
    sumTotal += totalWords;
    sumErrors += totalErrors;
    printf("%6d %6d %s\n", totalWords, totalErrors, FILENAME);
}

END {
    if (sumTotal > 0) {
	printf("==================================\n");
	wer = (sumErrors/sumTotal) * 100.0;
	printf("%6d  %6d   WER: %.2f%%\n",  sumTotal, sumErrors, wer);
    }
}
